import org.jetbrains.compose.desktop.application.dsl.TargetFormat

plugins {
  id 'org.eduardoleolim.kotlin-application-conventions'
  id 'org.jetbrains.compose' version '1.5.11'
}

dependencies {
  implementation project(':core')
  implementation(compose.desktop.currentOs) {
    exclude group: 'org.jetbrains.compose.material'
  }
  implementation compose.materialIconsExtended
  implementation compose.material3
  implementation "cafe.adriel.voyager:voyager-navigator:$voyagerVersion"
  implementation "cafe.adriel.voyager:voyager-screenmodel:$voyagerVersion"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-swing:$coroutinesVersion"
  implementation("com.github.Dansoftowner:jSystemThemeDetector:$themeDetectorVersion") {
    exclude group: 'org.slf4j'
  }
}

kotlin {
  jvmToolchain(17)
}

compileKotlin {
  compilerOptions {
    freeCompilerArgs = ["-Xcontext-receivers=true"]
  }
}

compose.desktop {
  application {
    mainClass = 'org.eduardoleolim.organizadorpec660.app.MainKt'
    args('--database=./data/organizador-pec-6-60.db')

    nativeDistributions {
      packageName = 'Organizador PEC-6-60'
      description = 'Organizador de formatos PEC-6-60'
      copyright = 'Copyright Â© 2023 Angel Eduardo Martinez Leo Lim. All rights reserved.'

      includeAllModules = true

      windows {
        iconFile.set(file('src/main/resources/assets/icon.ico'))
      }
      linux {
        iconFile.set(file('src/main/resources/assets/icon.png'))
      }

      targetFormats(TargetFormat.Dmg, TargetFormat.Msi, TargetFormat.Deb)
    }
  }
}

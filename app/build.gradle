import org.eduardoleolim.tasks.addmoduleinfo.AddModuleInfoTask
import org.gradle.internal.os.OperatingSystem

plugins {
  id 'org.eduardoleolim.kotlin-application-conventions'
  id 'org.beryx.jlink' version '3.0.1'
  id 'org.jetbrains.compose' version '1.5.10'
}

description = 'Organizador de formatos PEC-6-60'

dependencies {
  implementation project(':core')
  implementation(compose.desktop.currentOs) {
    exclude group: 'org.jetbrains.compose.material'
  }
  implementation compose.materialIconsExtended
  implementation compose.material3
}

ext {
  moduleName = 'org.eduardoleolim.organizadorpec660.app'
}

kotlin {
  jvmToolchain(17)
}

application {
  // Define the main class for the application.
  mainClass = 'org.eduardoleolim.organizadorpec660.app.AppKt'
  applicationDefaultJvmArgs = ['-Dfile.encoding=UTF-8']
}

jlink {
  moduleName = project.moduleName
  mergedModuleName = "${project.moduleName}.merged.module"

  forceMerge("sqlite", "kotlinx-coroutines-core")
  addExtraDependencies("kotlin")

  mergedModule {
    requires 'java.desktop'
    requires 'java.sql'

    uses 'kotlin.reflect.jvm.internal.impl.resolve.ExternalOverridabilityCondition'
    uses 'kotlin.reflect.jvm.internal.impl.util.ModuleVisibilityHelper'
  }

  launcher {
    name = 'Organizador PEC-6-60'
    args = ['--database={{BIN_DIR}}/../database/organizador-pec-6-60.db']
  }

  jpackage {
    args = ['--database={{BIN_DIR}}/database/organizador-pec-6-60.db']

    installerOptions = [
      '--description', description,
      '--copyright', 'Copyrigth 2023-2023 Ángel Eduardo Martínez Leo Lim',
      '--vendor', 'Ángel Eduardo Martínez Leo Lim'
    ]

    if (OperatingSystem.current().windows) {
      installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
      imageOptions += ['--icon', "src/main/resources/assets/icon.ico"]
    }
  }

  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
}

tasks.register('addModuleInfo', AddModuleInfoTask) {
  moduleName = project.moduleName
  jarPath = jlink.jlinkBasePath.get() + '/jlinkJars/app.jar'
  classpath = files(file(jlink.jlinkBasePath.get() + '/jlinkJars'))
  multiRelease = '17'
}

prepareModulesDir.finalizedBy addModuleInfo

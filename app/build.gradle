import tasks.AddModuleInfoTask

plugins {
  id 'org.eduardoleolim.kotlin-application-conventions'
  id 'org.beryx.jlink' version '2.26.0'
  id 'org.jetbrains.compose' version '1.4.3'
}

dependencies {
  implementation project(':core')
  implementation compose.desktop.currentOs
  implementation 'org.jetbrains.compose.material:material-icons-extended-desktop:1.4.3'
  implementation "org.jetbrains.compose.material3:material3-desktop:1.4.3"
}

ext {
  moduleName = 'org.eduardoleolim.organizadorpec660'
}

kotlin {
  jvmToolchain(17)
}

application {
  // Define the main class for the application.
  mainClass = 'org.eduardoleolim.app.AppKt'
}

jlink {
  moduleName = project.moduleName

  mergedModuleName = "${project.moduleName}.merged.module"
  forceMerge("sqlite")
  mergedModule {
    requires transitive 'java.desktop'
    requires transitive 'java.sql'
    uses 'java.sql.Driver'
    provides 'java.sql.Driver' with 'org.sqlite.JDBC'

    uses 'kotlin.reflect.jvm.internal.impl.resolve.ExternalOverridabilityCondition'
    uses 'kotlin.reflect.jvm.internal.impl.util.ModuleVisibilityHelper'
  }

  launcher {
    name = 'OrganizadorPec660'
    args = ['--database={{BIN_DIR}}/../database/organizador-pec-660.db']
  }
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
}

tasks.register('addModuleInfo', AddModuleInfoTask) {
  moduleName = project.moduleName
  jarPath = jlink.jlinkBasePath.get() + '/jlinkJars/app.jar'
  classpath = files(file(jlink.jlinkBasePath.get() + '/jlinkJars'))
  multiRelease = '17'
}

prepareModulesDir.finalizedBy addModuleInfo
